# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0


import os

Import('env')

env.Append(CPPDEFINES = 'NEED_DATA_STORE')
env['_ALLJOYN_CONFIG_'] = True
env['OBJDIR_SERVICES_CONFIG'] = env['OBJDIR'] + '/services/config'

# Make config library and header file paths available to the global environment
env.Append(LIBPATH = '$DISTDIR/config/lib');
env.Append(CPPPATH = '$DISTDIR/config/inc');

if not env.has_key('_ALLJOYN_ABOUT_') and os.path.exists('../../../core/alljoyn/services/about/SConscript'):
    env.SConscript('../../../core/alljoyn/services/about/SConscript')

if not env.has_key('_ALLJOYN_SERVICES_COMMON_') and os.path.exists('../services_common/SConscript'):
    env.SConscript('../services_common/SConscript')

if 'cpp' in env['bindings'] and not env.has_key('_ALLJOYNCORE_') and os.path.exists('../../core/alljoyn/alljoyn_core/SConscript'):
    env.SConscript('../../../core/alljoyn/alljoyn_core/SConscript')

if 'java' in env['bindings'] and not env.has_key('_ALLJOYN_JAVA_') and os.path.exists('../../core/alljoyn/alljoyn_java/SConscript'):
    env.SConscript('../../../core/alljoyn/alljoyn_java/SConscript')

config_env = env.Clone()

for b in config_env['bindings']:
    if os.path.exists('%s/SConscript' % b):
        config_env.VariantDir('$OBJDIR_SERVICES_CONFIG/%s' % b, b, duplicate = 0)

config_env.SConscript(['$OBJDIR_SERVICES_CONFIG/%s/SConscript' % b for b in env['bindings'] if os.path.exists('%s/SConscript' % b) ],
                      exports = ['config_env'])