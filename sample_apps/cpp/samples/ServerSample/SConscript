# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0


Import('env', 'cobjs')
local_env = env.Clone()

srcs = ['ServerSample.cc', 'OptParser.cc']
objs = local_env.Object(srcs)
objs.extend(cobjs)

if '_CONFIG_' in local_env["CPPDEFINES"] :
    configSrcs = 'ConfigServiceListenerImpl.cc'
    configObjs = local_env.Object(configSrcs)
    objs.extend(configObjs)

if '_CONTROLPANEL_' in local_env["CPPDEFINES"] : 
    local_env.Append(CPPPATH = local_env.Dir('generated').srcnode())
    local_env.Append(CPPPATH = local_env.Dir('provided').srcnode())

    genobjs = local_env.SConscript('generated/SConscript', {'env': local_env})
    providedobjs = local_env.SConscript('provided/SConscript', {'env': local_env})
   
    objs.extend(genobjs)
    objs.extend(providedobjs)
    
if '_TESTMODE_' in local_env["CPPDEFINES"] :
    notifSrcs = ['NotificationReceiverImpl.cc', 'NotificationTesterImpl.cc']
    notifObjs = local_env.Object(notifSrcs)
    objs.extend(notifObjs)

if '_ONBOARDING_' in local_env["CPPDEFINES"] :
    onboardingSrcs = 'OnboardingControllerImpl.cc'
    onboardingObjs = local_env.Object(onboardingSrcs)
    objs.extend(onboardingObjs)

local_env.Install('$SAMPLE_APPS_DISTDIR/bin', 'FactoryServerSample.conf')
local_env.Install('$SAMPLE_APPS_DISTDIR/bin', 'ServerSample.conf')
prog = local_env.Program('ServerSample', objs)

Return('prog')