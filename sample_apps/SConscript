# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0



import os

Import('env')

env['_ALLJOYN_SERVER_SAMPLE_'] = True
env['OBJDIR_SERVICES_SAMPLE'] = env['OBJDIR'] + '/services/sample_apps'

build_all = ARGUMENTS.get('ALL', 1)
if 'java' in env['bindings']:
    build_all = 1

if int(build_all):
    build_notification = 1
    build_cps          = 1
else:
    build_notification = int(ARGUMENTS.get('NOTIFICATION', 0))
    build_cps          = int(ARGUMENTS.get('CONTROLPANEL', 0))

env.Append(CPPDEFINES = 'NEED_DATA_STORE')

if not env.has_key('_ALLJOYN_SERVICES_COMMON_') and os.path.exists('../services_common/SConscript'):
    env.SConscript('../services_common/SConscript')

if build_notification:
    env.Append(CPPDEFINES = '_NOTIFICATION_')
    if not env.has_key('_ALLJOYN_NOTIFICATION_') and os.path.exists('../notification/SConscript'):
        env.SConscript('../notification/SConscript')

if build_cps:
    env.Append(CPPDEFINES = '_CONTROLPANEL_')
    if not env.has_key('_ALLJOYN_CONTROLPANEL_') and os.path.exists('../controlpanel/SConscript'):
        env.SConscript('../controlpanel/SConscript')

server_sample_env = env.Clone()

# ASABASE-452, ASACORE-1419
# Even though we have deprecated the About Service code the onboarding service
# is designed so a developer can use the deprecated AboutService or the new
# About Feature code. So the onboarding service can continue to support the
# deprecated methods we must turn off the deprecated-declarations warning.
if server_sample_env['OS_GROUP'] == 'posix':
    server_sample_env.Append(CXXFLAGS = ['-Wno-deprecated-declarations'])

for b in server_sample_env['bindings']:
    if os.path.exists('%s/SConscript' % b):
        server_sample_env.VariantDir('$OBJDIR_SERVICES_SAMPLE/%s' % b, b, duplicate = 0)

server_sample_env.SConscript(['$OBJDIR_SERVICES_SAMPLE/%s/SConscript' % b for b in env['bindings'] if os.path.exists('%s/SConscript' % b) ],
                   exports = ['server_sample_env'])