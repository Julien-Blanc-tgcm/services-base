# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0

#

Import('env', 'CheckCXXFlag')
import os

# Darwin specific flags
env.Append(CPPDEFINES=['QCC_OS_DARWIN'])

sconf_dir = env['BUILDDIR'] + '/sconf'
sconf_log = sconf_dir + '/sconf.log'
config = Configure(env, conf_dir = sconf_dir, log_file = sconf_log, custom_tests = { 'CheckCXXFlag' : CheckCXXFlag })
if not config.CheckCXXFlag('-std=c++11'):
   if not config.CheckCXXFlag('-std=c++0x'):
      print '*** Compiler too old to build AllJoyn.  Aborting.'
      Exit(1)
env = config.Finish()

env.Append(CFLAGS=['-Wall',
                   '-pipe',
                   '-std=c99',
                   '-fno-strict-aliasing',
                   '-Wno-long-long'])

env.Append(CXXFLAGS=['-Wall',
                     '-Werror=non-virtual-dtor',
                     '-pipe',
                     '-fno-exceptions',
                     '-fno-strict-aliasing',
                     '-Wno-deprecated'])

# Debug/Release Variants
if env['VARIANT'] == 'debug':
   env.Append(CFLAGS='-g')
   env.Append(CXXFLAGS='-g')
   env.Append(JAVACFLAGS='-g -Xlint -Xlint:-serial')
   print 'Using debug settings for darwin build...'
else:
   env.Append(CFLAGS='-O3')
   env.Append(CXXFLAGS='-O3')
   env.Append(LINKFLAGS='')
   env.Append(JAVACFLAGS='-Xlint -Xlint:-serial')
   print 'Using release settings for darwin build...'

env.SConscript('${CPU}/SConscript')

env.Append(CPPDEFINES=['MECHANISM_PIPE'])
env.AppendUnique(LIBS =['stdc++', 'pthread'])

print 'Using builtin crypto'