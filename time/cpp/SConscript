# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0

Import('tsenv')
 
if not tsenv.has_key('_ALLJOYNCORE_'):
    tsenv.Append(LIBS = ['alljoyn'])
    if tsenv['BR'] == 'on' :
        brFile = tsenv.subst('$DISTDIR') + '/cpp/lib/BundledRouter.o'
        tsenv['brobj'] = File(brFile)
        tsenv['ajrlib'] = 'ajrouter'
    if tsenv['OS'] == 'openwrt':
        tsenv.AppendUnique(LIBS = [ 'stdc++', 'pthread' ])

tsenv['TS_DISTDIR'] = tsenv['DISTDIR'] + '/time'

tsenv.Install('$TS_DISTDIR/inc/alljoyn/time', tsenv.Glob('inc/alljoyn/time/*.h'))
tsenv.Install('$TS_DISTDIR/lib', tsenv.SConscript('src/SConscript', exports = ['tsenv']))
if tsenv['BUILD_SERVICES_SAMPLES'] == 'on':
    tsenv.Install('$TS_DISTDIR/bin', tsenv.SConscript('samples/SConscript', exports = ['tsenv']))

# Build docs
installDocs = tsenv.SConscript('docs/SConscript', exports = ['tsenv'])
tsenv.Depends(installDocs, tsenv.Glob('$TS_DISTDIR/inc/alljoyn/time/*.h'));