# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0


import os

Import('env')

env['_ALLJOYN_ONBOARDING_'] = True
env['OBJDIR_SERVICES_ONBOARDING'] = env['OBJDIR'] + '/services/onboarding'
env.Append(CPPDEFINES = 'NEED_DATA_STORE')

# Make config library and header file paths available to the global environment
env.Append(LIBPATH = '$DISTDIR/onboarding/lib');
env.Append(CPPPATH = '$DISTDIR/onboarding/inc');

if not env.has_key('_ALLJOYN_SERVICES_COMMON_') and os.path.exists('../services_common/SConscript'):
    env.SConscript('../services_common/SConscript')

onb_env = env.Clone()

# ASABASE-452, ASACORE-1419
# Even though we have deprecated the About Service code the onboarding service
# is designed so a developer can use the deprecated AboutService or the new
# About Feature code. So the onboarding service can continue to support the
# deprecated methods we must turn off the deprecated-declarations warning.
if onb_env['OS_GROUP'] == 'posix':
    onb_env.Append(CXXFLAGS = ['-Wno-deprecated-declarations'])

for b in onb_env['bindings']:
    if os.path.exists('%s/SConscript' % b):
        onb_env.VariantDir('$OBJDIR_SERVICES_ONBOARDING/%s' % b, b, duplicate = 0)

onb_env.SConscript(['$OBJDIR_SERVICES_ONBOARDING/%s/SConscript' % b for b in env['bindings'] if os.path.exists('%s/SConscript' % b) ],
                   exports = ['onb_env'])