# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0


Import('onb_env')

local_env = onb_env.Clone()

vars = Variables()
vars.Update(local_env)
Help(vars.GenerateHelpText(local_env))

if local_env['OS'] == 'openwrt':
   local_env.Append(CPPDEFINES = '_OPEN_WRT_')

local_env.Append(CPPPATH = ['.'])

local_env.Prepend(LIBS = [ 'alljoyn_onboarding',
                           'alljoyn_config',
                           'alljoyn_about',
                           'alljoyn_services_common' ])

if local_env['BR'] == 'on' and local_env.has_key('brobj') and local_env.has_key('ajrlib'):
    # Build apps with bundled daemon support
    local_env.Prepend(LIBS = [local_env['brobj'], local_env['ajrlib']])

local_env.Append(CPPPATH = local_env.Dir('$APP_COMMON_DIR/cpp/samples_common').srcnode())
local_env.VariantDir('AppCommon', '$APP_COMMON_DIR/cpp/samples_common/', duplicate = 0)
cobjs = local_env.SConscript('AppCommon/SConscript', {'env': local_env})

# Sample programs

sample_dirs = [
    'OnboardingClientSample'
    ]

exports = { 'env': local_env, 'cobjs': cobjs }

progs = [ local_env.SConscript('%s/SConscript' % sampleapp, exports = exports) for sampleapp in sample_dirs ]

Return('progs')
